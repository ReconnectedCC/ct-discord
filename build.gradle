plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "discordbridge" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    mavenCentral()

    maven { url 'https://maven.wispforest.io' }
}
ext {
    reactor_bom_version = '2023.0.8'
    jackson_datatype_jsr310_version = '2.17.0'
    discordJsonVersion="1.7.10"
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    annotationProcessor modImplementation("io.wispforest:owo-lib:${project.owo_version}")

    include implementation("net.dv8tion:JDA:${project.jda_version}") {
        exclude module: 'opus-java'
    }
    include implementation("club.minnced:discord-webhooks:${project.webhooks_version}")

    // JDA dependencies
    include "org.json:json:20230227"
    include "com.squareup.okio:okio:1.17.2"
    include "com.squareup.okhttp3:okhttp:3.13.0"
    include "org.apache.commons:commons-collections4:4.4"
    include "org.jetbrains.kotlin:kotlin-stdlib:1.6.21"
    include "com.neovisionaries:nv-websocket-client:2.14"
    include "net.sf.trove4j:trove4j:3.0.3"
    include 'com.fasterxml.jackson.core:jackson-databind:2.13.4.1'
    include "com.fasterxml.jackson.core:jackson-annotations:2.13.1"
    include "com.fasterxml.jackson.core:jackson-core:2.13.1"

    /*include implementation("com.discord4j:discord4j-core:${project.discord4j_version}")
    // Discord4j dependencies
    include implementation("com.discord4j:discord4j-rest:${project.discord4j_version}")
    include implementation("com.discord4j:discord4j-common:${project.discord4j_version}")
    include platform("io.projectreactor:reactor-bom:$reactor_bom_version")
    include implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_datatype_jsr310_version")
    include implementation("com.discord4j:discord-json:$discordJsonVersion")
    include implementation("io.projectreactor:reactor-core:3.6.8")*/



    include modImplementation("net.kyori:adventure-platform-fabric:${project.kyori_adventure_fabric_version}")
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.

        maven {
            name = "AlexDevs"
            url = "https://maven.alexdevs.me/releases"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}
